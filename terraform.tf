# https://registry.terraform.io/providers/OpenNebula/opennebula/latest/docs

# Terraform provider dependency:
terraform {
  required_providers {
    opennebula = {
      source = "OpenNebula/opennebula"
      version = "~> 1.2"
    }
  }
}

# Terraform provider parameters definition:
provider "opennebula" {
  endpoint      = "${var.opennebula_endpoint}"
  username      = "${var.opennebula_username}"
  password      = "${var.opennebula_token}"
}

# Resource (OS image) definition:
resource "opennebula_image" "os-image" {
    name = "${var.vm_image_name}"
    datastore_id = "${var.vm_imagedatastore_id}"
    persistent = false
    path = "${var.vm_image_url}"
    permissions = "600"
}

# Resource (VM) definition:
resource "opennebula_virtual_machine" "vmnode" {
  count = var.vm_count
  name = "${var.vm_machine_name}-${count.index + 1}"
  description = "Main node VM"
  cpu = 1
  vcpu = 1
  memory = 1024
  permissions = "600"
  group = "users"

  context = {
    NETWORK  = "YES"
    HOSTNAME = "$NAME"
    SSH_PUBLIC_KEY = "${var.ssh_public_key}"
    START_SCRIPT = "echo 'Wuz here :-)' >> /etc/my-message"
  }
  os {
    arch = "x86_64"
    boot = "disk0"
  }
  disk {
    image_id = opennebula_image.os-image.id
    target   = "vda"
    size     = 12000 # 12GB
  }
  graphics {
    listen = "0.0.0.0"
    type   = "vnc"
  }
  # The Network Interface Controller is connected to 'vlan173' network (147.228.173.0/24) which has ID = 3
  nic {
    network_id = var.vm_network_id
  }

  connection {
    type = "ssh"
    user = "root"
    host = "${self.ip}"
    private_key = "${file("/var/iac-dev-container-data/id_ecdsa")}"
  }

  provisioner "file" {
    source = "init-scripts/"
    destination = "/tmp"
  }

  provisioner "remote-exec" {
    inline = [
      "export INIT_USER=${var.vm_admin_user}",
      "export INIT_PUBKEY='${var.ssh_public_key}'",
      "export INIT_LOG=${var.vm_node_init_log}",
      "export INIT_HOSTNAME=${self.name}",
      "touch ${var.vm_node_init_log}",
      "sh /tmp/init-start.sh",
      "sh /tmp/init-node.sh",
      "sh /tmp/init-users.sh",
      "sh /tmp/init-finish.sh"
    ]
  }
}

# Outputs

output "vm_nodes_ips" {
  value = opennebula_virtual_machine.vmnode.*.ip
}

resource "local_file" "host_inventory" {
  content = templatefile("inventory.template",
  {
    vm_admin_user = var.vm_admin_user,
    ip_list = opennebula_virtual_machine.vmnode.*.ip
  })
  filename = "ansible/inventory.yml"
}

resource "null_resource" "ansible-provisioner" {
  provisioner "local-exec" {
    # Start provisioning all nodes:
    command = "ansible-playbook -i inventory.yml site.yml"
    working_dir = "${path.module}/ansible"
    environment = {
      ANSIBLE_HOST_KEY_CHECKING = "False"
    }
  }
  depends_on = [local_file.host_inventory]
}

#
# EOF
#